plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dagger.hilt.android.plugin"
    id "kotlin-kapt"
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.gavinferiancek.tasky"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())

        buildConfigField "String", "API_KEY", "\"${properties.getProperty("API_KEY")}\""
        buildConfigField "String", "BASE_URL", "\"${properties.getProperty("BASE_URL")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Accompanist
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    // AndroidX
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    // Compose
    implementation "androidx.activity:activity-compose:1.5.1"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.2"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Crypto (EncryptedSharedPreferences)
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    // Desugaring (For Java DateTime)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // LeakCanary
    implementation "com.squareup.leakcanary:leakcanary-android:2.9.1"

    // Moshi
    implementation "com.squareup.moshi:moshi-adapters:1.13.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Testing
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"
    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.1.3"

    // Vanpra (Material Dialogs)
    implementation "io.github.vanpra.compose-material-dialogs:core:$material_dialogs_version"
    implementation "io.github.vanpra.compose-material-dialogs:datetime:$material_dialogs_version"
}